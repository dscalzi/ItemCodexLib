plugins {
	id 'com.jfrog.bintray' version '1.8.4'
	id 'java'
	id 'eclipse'
	id 'maven'
	id 'maven-publish'
}

group = 'com.dscalzi'
version = '1.0.0-pre.7'
archivesBaseName = 'ItemCodexLib'

description = """Library to dynamically retrieve items by alias, enum, or legacy data."""

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compile group: 'com.google.code.gson', name: 'gson', version:'2.8.5'
    compileOnly group: 'org.spigotmc', name: 'spigot-api', version:'1.14.3-R0.1-SNAPSHOT'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

configurations {
    testImplementation.extendsFrom compileOnly
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    baseName = project.name
    manifest {
        attributes 'Implementation-Version': version, 'Specification-Version': version
    }
}

build {
    dependsOn clean
    dependsOn jar

    jar.mustRunAfter clean
}

bintrayUpload {
    dependsOn build
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
	key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ["BintrayRelease"]
    pkg {
        repo = 'maven'
        name = 'ItemCodexLib'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/dscalzi/ItemCodexLib.git'
        publish = true
        version {
            name = project.version
            released = new Date()
            vcsTag = 'v' + project.version
        }
    }
}

publishing {
    publications {
        BintrayRelease(MavenPublication) {
            from components.java
            groupId group
            artifactId archivesBaseName
            version version
            artifact javadocJar
            artifact sourcesJar
        }
    }
}